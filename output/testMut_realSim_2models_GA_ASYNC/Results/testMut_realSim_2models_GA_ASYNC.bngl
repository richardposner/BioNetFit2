# A simple model exMutM2
#
begin model
begin parameters
	# simulation parameters
	# fraction of a single cell to be considered in a stochastic simulation
	f 0.1 # [=] dimensionless, 0<f<=1
	# physical constants
	# Avogadro constant
	NA  6.02214e23 # [=] number of molecules per mol
	# Scenario 1: Cells are adherant and grown to confluence in a cell culture dish
	# volume of extracellular fluid per cell
	#   Assumptions:
	#    - 15 cm culture dishes (cells 95% confluent)
	#    - 1.2e5 cells per cm^2
	#    - growth surface area of 140 cm^2
	#    - 1.7e7 cells per dish
	#    - 0.25 mL of medium per square cm of growth surface
	#    - 35 mL of medium per dish
	Vecf 1.9e-9*f  #razi changed to make distiction with model M1, originally it was  2.1e-9*f # [=] L per cell
	# ligand concentration
	EGF_conc_nM  1.0 # [=] nM
	# number of ligands per cell (derived)
	# Convert from nM to Molar
	EGF_conc = EGF_conc_nM * 1e-9 # M
	EGF_copy_number = EGF_conc*(NA*Vecf) # [=] number of molecules per cell
	# receptor copy number
	EGFR_copy_number  f*1.0e5 # [=] number of molecules per cell
	# binding parameters
	# a free ligand reversibly binds a free receptor site
	# equilibrium dissociation constant
	# KD1 is specified as being free. It has a matching option in the .conf file.
	KD1 0.022325 # [=] nM
	#KD1 = KD1_root*KD1_root 
	# reverse rate constant (derived)
	#km1 is specified as being free. It has a matching option in the .conf file.
	km1 0.482630 # [=] /s
	#km1 = km1_root*km1_root 
	# forward rate constant (derived)
	kp1=(km1/KD1) # [=] /nM/s
	# receptor-receptor interaction
	# dimensionless equilibrium association constant
	# K2RT is specified as being free. It has a matching option in the .conf file.
	K2RT 2.334969 # [=] dimensionless
	#K2RT = K2RT_root*K2RT_root
	#   a value of 0.1 => weak crosslinking
	#   a value of 1.0 => moderate crosslinking
	#   a value of 10.0 => strong crosslinking
	# reverse rate constant (derived)
	km2 0.020347 # [=] /s
	#km2 = km2_root*km2_root	
	# forward rate constant (derived)
	kp2=K2RT*km2/EGFR_copy_number # [=] /nM/s
	kp3=kp2*0.2
	# phosphorylation rate constant
	# kphos is specified as being free. It has a matching option in the .conf file.
	#razi, changed for test, is a free parameter in exMutM1 
	#kphos_fixed = 1
	#changed for testing 
	kphos 1.227189 # [=] /s
	#kphos = kphos_root*kphos_root
	# dephosphorylation rate constant
	# kdephos is specified as being free. It has a matching option in the .conf file.
	kdephos 0.197261 # [=] /s
	#kdephos = kdephos_root*kdephos_root
	# A boolean value indicating whether or not EGF is present. This
	# is used to set the forward rate constant for ligand binding in
	# the "func()" function. In pre-equilibration this value is set to
	# 0 to set the rate of ligand binding to 0. After equilibration
	# the value will be set to 1 to let ligand bind.
	Ligand_isPresent 0
end parameters
begin molecule types
	# ligand
	L(r) 
	# receptor
	R(l,r,Y~0~P) 
end molecule types
#bound ligand and p
begin seed species
	L(r)  EGF_copy_number
	R(l,r,Y~0)  EGFR_copy_number
end  seed species
begin observables
	# total number of ligands
	Molecules Ltot L()
	# number of free ligands
	Species freeL L(r) 
	# total number of receptors
	Molecules Rtot R()
	# number of bound ligands = Ltot - freeL
	# number of free receptors
	#Species freeR R(l,r) 
	# number of monomeric (unclustered) receptors
	#Species Rmon R==1 
	# number of receptor dimers
	Species Rdim R==2
	# number of ligand-induced receptor aggregates
	#   = number of receptor clusters
	#   = number of complexes containing more than 1 receptor 
	#Species n_agg_gt1 R>1
	# number of ligand-receptor bonds
	#   = number of ligand-occupied receptor sites
	#   = number of receptor-occupied ligand sites
	Molecules RLbonds L(r!1).R(l!1) # = R(l!+) = L(r!+)
	# number of receptors in clusters = Rtot - R1
	# average size of a receptor cluster (of size >1) 
	#   = (# of receptors in clusters)/n_agg_gt1
	#   = (Rtot - R1)/n_agg_gt1
	# number of phosphorylated receptors
	Molecules pR R(Y~P)
end observables
begin functions
# This function evaluates to 0 if Ligand_isPresent = 0. The function evaluates to kp1 if Ligand_isPresent = 1
# The function is used in the reaction rule governing ligand binding. After equilibration, Ligand_isPresent is
# set to 1 to enable ligand binding.
func() kp1 * Ligand_isPresent
end functions
begin reaction rules
	# ligand capture
	#   a free ligand binds a receptor with a free site
	L(r)+R(l)<->L(r!1).R(l!1) func(),km1
	# receptor dimerization
	R(l!+,r)+R(l!+,r)->R(l!+,r!1).R(l!+,r!1) kp2
	# receptor-receptor bond dissociation
	R(r!1).R(r!1)->R(r)+R(r) km2*0.2 #razi changed to make distiction with model 1, was originally km2
	# receptor dimerization (no ligand)
	R(l,r) + R(l,r) -> R(l,r!1).R(l,r!1) kp3
	# dimer-mediated receptor phosphorylation
#	R(r!+,Y~0)->R(r!+,Y~P) kphos_fixed
	R(r!+,Y~0)->R(r!+,Y~P) kphos
	# dephosphorylation
	R(Y~P)->R(Y~0) kdephos
end reaction rules
end model
# actions
# Generate the network
generate_network();
# Simulate for 600 seconds to reach equilibrium
# razi changed, originally was: simulate({method=>"ode",t_start=>0,t_end=>600,n_steps=>1,suffix=>"exMutM2_equil"})
simulate({method=>"ode",t_start=>0,t_end=>600,n_steps=>1})
#simulate({method=>"random",t_start=>0,t_end=>600,n_steps=>1,suffix=>"exMutM2_equil"})
# Set Ligand_isPresent to 1 to enable ligand binding by means of the "func()" function
setParameter("Ligand_isPresent",1)
# Simulate for 60 seconds. This simulation output is fit to the data in example3.exp
# razi changed, originally was: simulate({method=>"ode",t_start=>0,t_end=>60,sample_times=>[0,5,10,15,20,25,30,35,40,45,50,55,60],prefix=>"exMutM2"})
simulate({method=>"ode",t_start=>0,t_end=>60,sample_times=>[0,5,10,15,20,25,30,35,40,45,50,55,60],prefix=>"exMutM2"})
#simulate({method=>"random",t_start=>0,t_end=>60,sample_times=>[0,5,10,15,20,25,30,35,40,45,50,55,60],prefix=>"exMutM2"})
